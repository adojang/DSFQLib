* Definition file for TimEx - MITLL generic cell library with 250 uA JJs and PTL I/O
* Author:   CJ Fourie
* Last mod: 14 April 2020
*
* This work was supported by the Office of the Director of National          
* Intelligence (ODNI), Intelligence Advanced Research Projects Activity        
* (IARPA), via the U.S. Army Research Office grant W911NF-17-1-0120.           
********************************************************************************
$Parameters
SourceType      = current
*SourceAmplitude = 600E-6
*SourceRiseTime  = 10E-12
*SourceFallTime  = 10E-12
SourceAmplitude = 1000E-6
SourceRiseTime  = 1E-12
SourceFallTime  = 1E-12
TimeFirstStable = 20E-12
WaitForStateChange = 40E-12
VerilogStableTime = 10E-12
VerilogWaitTime = 10E-12
SlidingIntegratorLength = 10E-12
PulseDetectThreshold = 0.5
PulseFluxonFraction = 0.50
CTDependencyThreshold = 0.1E-12
MaxDelayChange = 0.75E-12
MaxSelfDelayChange = 1.0E-12
InputChainDelay = 7.5E-12
IOFullFluxon = FALSE              // I/O's are PTL; pulses not full fluxons
$End

*$Control
*Sweep Bias 0.6 0.1 1.4  // Sweep the "Bias" variable in netlists
*Sweep Bias 0.9 0.1 1.1  // Sweep the "Bias" variable in netlists
*$End

$DefaultLoadIn
.SUBCKT defloadin 2 5 
Tload 2 0 5 0 LOSSLESS Z0=5 TD=10p
.ends defloadin
$End

$DefaultLoadOut
.SUBCKT defloadout 2 5 
Tload 2 0 5 0 LOSSLESS Z0=5 TD=50p
.ends defloadout
$End

$DefaultSink
.SUBCKT defsink 2
B1    1  9  jjmitll100     area=1
B2    4  8  jjmitll100     area=1
B3    5  7  jjmitll100     area=1
IB1   0  10 pwl(0      0 5p 145u)
L1    2  1  0.2p      
L2    1  3  4.3p      
L3    3  4  4.6p      
L4    4  5  6.0p      
L5    5  6  1.3p      
LP1   0  9  0.34p     
LP2   0  8  0.06p     
LP3   0  7  0.03p     
LPR1  3  10 0.2p      
LRB1  9  11 0.5p      
LRB2  8  12 1p        
LRB3  7  13 1p        
RB1   11 1  15.4      
RB2   12 4  11.3      
RB3   13 5  11.3      
Rsink 6  0  2
.model jjmitll100 jj(rtype=1, vg=2.8mV, cap=0.07pF, r0=160ohm, rn=16ohm, icrit=0.1mA)
.ends defsink
$End

$DefaultSource
.SUBCKT gensource 8 22 
* DC-SFQ
B1    1  2  jjmitll100 area=2.25 
B2    3  4  jjmitll100 area=2.25 
B3    5  6  jjmitll100 area=2.5 
IB1   0  2  pwl(0 0 5p 275uA)
IB2   0  5  pwl(0 0 5p 175uA)
L1    8  7  1p  
L2    7  0  3.9p  
L3    7  1  0.6p  
L4    2  3  1.1p  
L5    3  5  4.5p  
L6    5  11 2p  
Lp2   4  0  0.2p  
Lp3   6  0  0.2p  
LRB1  9  2  1p  
LRB2  10 4  1p  
LRB3  12 6  1p  
RB1   1  9  4.31  
RB2   3  10 4.31  
RB3   5  12 3.88  
* PTL-TX
B01   23 27 jjmitll100 area=2
B02   24 26 jjmitll100 area=1.62
IB01  0  30 pwl(0      0 5p 0.00023)
IB02  0  31 pwl(0      0 5p 8.2e-005)
L01   11 23 2.5e-012  
L02   23 24 3.3e-012  
L03   24 25 3.5e-013  
LP01  0  27 5e-014    
LP02  0  26 1.2e-013  
LPR01 23 30 2e-013    
LPR02 24 31 1.3e-012  
LRB01 27 28 1e-012    
LRB02 26 29 1e-012    
RB01  28 23 4.85      
RB02  29 24 6.3       
RINS  25 22 1.36      
.model jjmitll100 jj(rtype=1, vg=2.8mV, cap=0.07pF, r0=160, rn=16, icrit=0.1mA)
.ends gensource
$End